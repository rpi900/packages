# Maintainer: Matthew Hollingworth <matthew@rpi900.com>
pkgname=('linux-rpi900' 'linux-rpi900-headers')
_commit=af56bd8703ea1c55e670a7fc9f06b3f79018add6
pkgver=3.18.7
pkgrel=3
arch=('armv6h')
url='http://rpi900.com'
license=('GPL2')
makedepends=('xmlto' 'docbook-xsl' 'kmod' 'inetutils' 'bc' 'git')
options=('!strip')
source=(
	"PKGBUILDs-${_commit}.tar.gz::https://github.com/archlinuxarm/PKGBUILDs/archive/${_commit}.tar.gz"
	'PKGBUILDs.patch'
)
md5sums=(
	'e2f7ed69e5ed4acbbed63542dbe6f08c'
	'05da0e5c3f965d7737eafc2c6ca0d078'
)
prepare() {
	cd "${srcdir}/PKGBUILDs-${_commit}"
	patch -p1 -i "${srcdir}/PKGBUILDs.patch"
	cd core/linux-raspberrypi
	makepkg --nobuild
}
build() {
	cd "${srcdir}/PKGBUILDs-${_commit}/core/linux-raspberrypi"
	env PKGEXT='.pkg.tar.xz' PKGDEST="${srcdir}/PKGBUILDs-${_commit}/core/linux-raspberrypi" makepkg --noextract --nosign --clean
}
package_linux-rpi900() {
	pkgdesc='The Linux Kernel and modules - Raspberry Pi - patched for RPi900'
	depends=('coreutils' 'linux-firmware' 'module-init-tools>=3.16')
	optdepends=('crda: to set the correct wireless channels of your country')
	provides=('kernel26' "linux=${pkgver}" "linux-raspberrypi=${pkgver}" 'aufs_friendly' 'dnt900' 'rpi900-rtc')
	conflicts=('kernel26' 'linux' 'linux-raspberrypi' 'dnt900' 'rpi900-rtc')
	backup=('boot/config.txt' 'boot/cmdline.txt')
	replaces=('linux-raspberrypi-latest')
	install=linux-rpi900.install
	cd "${pkgdir}"
	bsdtar xf "${srcdir}/PKGBUILDs-${_commit}/core/linux-raspberrypi/linux-raspberrypi-${pkgver}-${pkgrel}-armv6h.pkg.tar.xz"
	rm -f .INSTALL .MTREE .PKGINFO
}
package_linux-rpi900-headers() {
	pkgdesc='Header files and scripts for building modules for linux kernel - Raspberry Pi'
	provides=("linux-headers=${pkgver}" "linux-raspberrypi-headers=${pkgver}")
	conflicts=('linux-headers' 'linux-raspberrypi-headers')
	replaces=('linux-raspberrypi-latest-headers')
	cd "${pkgdir}"
	bsdtar xf "${srcdir}/PKGBUILDs-${_commit}/core/linux-raspberrypi/linux-raspberrypi-headers-${pkgver}-${pkgrel}-armv6h.pkg.tar.xz"
	rm -f .INSTALL .MTREE .PKGINFO
}
