# Maintainer: Matthew Hollingworth <matthew@rpi900.com>
pkgname=rpi900
_dnt900_ver='0.3.6'
pkgver=$(pacman -Q linux-raspberrypi | sed 's/linux-raspberrypi \([0-9\.]*\)-\([0-9]*\)/\1/')
pkgrel=$(pacman -Q linux-raspberrypi | sed 's/linux-raspberrypi \([0-9\.]*\)-\([0-9]*\)/\2/')
pkgdesc='DNT900 line discipline module and startup service for RPi900'
arch=('armv6h' 'armv7h')
url='http://rpi900.com'
license=('GPL')
depends=('linux-raspberrypi>=3.18.3' "linux-raspberrypi=${pkgver}-${pkgrel}" 'rpirtscts')
makedepends=("linux-raspberrypi-headers=${pkgver}-${pkgrel}")
conflicts=('linux-rpi900')
backup=('etc/rpi900.conf')
install=rpi900.install
source=(
  "dnt900-${_dnt900_ver}.tar.gz::https://github.com/mholling/dnt900/archive/v${_dnt900_ver}.tar.gz"
  rpi900.conf
  rpi900.service
  99-rpi900.rules
)
md5sums=(
  '26974ac6b3487c8b71150fb47949a20e'
  'SKIP'
  'SKIP'
  'SKIP'
)
build() {
  cd "${srcdir}/dnt900-${_dnt900_ver}"
  KSRC="/usr/lib/modules/${pkgver}-${pkgrel}-ARCH/build/" make
}
package() {
  cd "${srcdir}/dnt900-${_dnt900_ver}"
  KSRC="/usr/lib/modules/${pkgver}-${pkgrel}-ARCH/build/" DESTDIR="${pkgdir}/usr" make modules_install
  mkdir -p "${pkgdir}/etc"
  cp "${srcdir}/rpi900.conf" "${pkgdir}/etc"
  mkdir -p "${pkgdir}/usr/lib/udev/rules.d"
  cp "${srcdir}/99-rpi900.rules" "${pkgdir}/usr/lib/udev/rules.d"
  mkdir -p "${pkgdir}/usr/lib/systemd/system"
  cp "${srcdir}/rpi900.service" "${pkgdir}/usr/lib/systemd/system"
}
